buildscript {
    ext.kotlin_version = '1.7.10'
    ext.ktlint_version = '0.44.0'
    ext.coroutines_version = '1.6.4'
    ext.ktlint_gradle_version = '11.0.0'
    // https://github.com/cashapp/sqldelight/issues/1574
    ext.sqldelight_version = '1.5.2'
    ext.nav_version = "2.5.3"
    ext.fragment_version = "1.5.4"
    ext.lifecycle_version = "2.5.1"
    ext.dokka_version = "0.10.1"
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_gradle_version"
        classpath "com.squareup.sqldelight:gradle-plugin:$sqldelight_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

apply plugin: 'com.squareup.sqldelight'

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    // Temp fix for issue https://github.com/mockk/mockk/issues/281
    configurations.all {
        resolutionStrategy {
            force("org.objenesis:objenesis:2.6")
        }
    }
}

group = 'nl.tudelft'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':ipv8-jvm')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.7.7'

    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.5.31'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            "-opt-in=kotlin.Experimental,kotlin.ExperimentalUnsignedTypes",
            "-opt-in=kotlin.RequiresOptIn"
    ]
}

test {
    useJUnit()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

sqldelight {
    Database {
        packageName = "nl.tudelft.eurotoken.sqldelight"
        sourceFolders = ["sqldelight"]
        schemaOutputDirectory = file("src/main/sqldelight/databases")
    }
}

application {
    mainClassName = 'MainKt'
}

run {
    standardInput = System.in
}